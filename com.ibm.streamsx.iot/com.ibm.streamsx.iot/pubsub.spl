/*
# Licensed Materials - Property of IBM
# Copyright IBM Corp. 2015,2016
 */

/**
 * Operators to use in conjunction with
 * [com.ibm.streamsx.iotf.apps::IotfOrganization].
 *
 * `IotfOrganziation` publishes streams for all the
 * events associated with devices, namely events, commands
 * and status updates.
 *
 * A single copy of `IotfOrganization` can be run and then
 * multiple analytic applications subscribe to streams as
 * needed. This allows separation of the IBM Watson IoT
 * Platform connection information (application key) from
 * the analytical applications.
*/

 namespace com.ibm.streamsx.iotf;

use com.ibm.streamsx.topology.topic::Publish;
use com.ibm.streamsx.topology.topic::Subscribe;

/**
 * Subscribe to device events published under
 * the streams topic `iotf/device/events`.
 *
 * This matches the stream topic published by
 * [com.ibm.streamsx.iotf.apps::IotfOrganization].
 *
 * Filtering on `typeId` and `eventId` is supported,
 * defaulting to no filtering.
 * 
 * @param typeIds Device types to include. Defaults
 * to an empty list meaning all device types.
 * @param eventIds Event identifiers (types) to include. Defaults
 * to an empty list meaning all event identifiers.
 * 
 * @output Events Device events
 */
public composite EventsSubscribe(output stream<DeviceEvent> Events)
{
   param
     expression<list<rstring>> $typeIds : [];
     expression<list<rstring>> $eventIds : [];

   graph
     stream<DeviceEvent> EventsAll = Subscribe()
     {
        param
          topic: "iotf/device/events";
          streamType: DeviceEvent;
     }

     // Currently we are filtering on the subscribe side
     // filter will be pushed to the publish side in the future
     stream<DeviceEvent> Events = Filter(EventsAll) {
       param
        filter:
           ((size($typeIds) == 0) || (typeId in $typeIds))
           &&
           ((size($eventIds) == 0) || (eventId in $eventIds));
     }
}

/**
 * Subscribe to device commands published under
 * the streams topic `iotf/device/commands/sent`.
 *
 * This matches the stream topic published by
 * [com.ibm.streamsx.iotf.apps::IotfOrganization].
 *
 * Filtering on `typeId` and `cmdId` is supported,
 * defaulting to no filtering.
 * 
 * @param typeIds Device types to include. Defaults
 * to an empty list meaning all device types.
 * @param cmdIds Command identifiers (types) to include. Defaults
 * to an empty list meaning all command identifiers.
 * 
 * @output Commands Device commands sent to devices
 */
public composite CommandsSubscribe(output stream<DeviceCmd> Commands)
{
   param
     expression<list<rstring>> $typeIds : [];
     expression<list<rstring>> $cmdIds : [];

   graph
     stream<DeviceCmd> CommandsAll = Subscribe()
     {
        param
          topic: "iotf/device/commands/sent";
          streamType: DeviceCmd;
     }

     // Currently we are filtering on the subscribe side
     // filter will be pushed to the publish side in the future
     stream<DeviceCmd> Commands = Filter(CommandsAll) {
       param
        filter:
           ((size($typeIds) == 0) || (typeId in $typeIds))
           &&
           ((size($cmdIds) == 0) || (cmdId in $cmdIds));
     }
}

/**
 * Subscribe to device status updates published under
 * the streams topic `iotf/device/statuses`.
 *
 * This matches the stream topic published by
 * [com.ibm.streamsx.iotf.apps::IotfOrganization].
 *
 * Filtering on `typeId` is supported,
 * defaulting to no filtering.
 * 
 * @param typeIds Device types to include. Defaults
 * to an empty list meaning all device types.
 * 
 * @output Statuses Device status updates
 */
public composite StatusesSubscribe(output stream<DeviceCmd> Statuses)
{
   param
     expression<list<rstring>> $typeIds : [];

   graph
     stream<DeviceStatus> StatusesAll = Subscribe()
     {
        param
          topic: "iotf/device/statuses";
          streamType: DeviceStatus;
     }

     // Currently we are filtering on the subscribe side
     // filter will be pushed to the publish side in the future
     stream<DeviceStatus> Statuses = Filter(StatusesAll) {
       param
        filter:
           ((size($typeIds) == 0) || (typeId in $typeIds));
     }
}

/**
 * Publish an event to be sent to a device.
 * via the streams topic `iotf/device/commands/send`.
 *
 * This matches the streams topic subscribed to by
 * [com.ibm.streamsx.iotf.apps::IotfOrganization].
 *
 * Any command on the input stream will be
 * sent as a device command assuming
 * [com.ibm.streamsx.iotf.apps::IotfOrganization]
 * is running, or an equivalant application.
*/
public composite CommandPublish(input stream<DeviceCmd> Commands) {
   graph
     () as PublishCommand = Publish(Commands) {
       param topic: "iotf/device/commands/send";
     }
}

